:Namespace convert
⎕io←0
Assert ← {⍺←'assertion failure' ⋄ 0∊⍵:⍺ ⎕SIGNAL 8 ⋄ shy←0}
CI ← { ⍝ ,⍣¯1 but better. CI = Concatinate Inverse
  _←Assert 1 1≡≢∘⍴¨⍺⍵
  _←Assert ⍺≤⍥≢⍵
  _←Assert ⍺≡⍵↑⍨≢⍺
  ⍵↓⍨≢⍺
}
CTI ← { ⍝ ,⍨⍣¯1 but better. CTI = Concatinate Tilda Inverse
  _←Assert 1 1≡≢∘⍴¨⍺⍵
  _←Assert ⍺≤⍥≢⍵
  _←Assert ⍺≡⍵↑⍨-≢⍺
  ⍵↓⍨-≢⍺
}

includes ← ⍬
includes ,←⊂ '#define RAYGUI_IMPLEMENTATION'
includes ,←⊂ '#include "raygui.h"'
includes ,←⊂ '#define PHYSAC_IMPLEMENTATION'
includes ,←⊂ '#define PHYSAC_NO_THREADS'
includes ,←⊂ '#define InitTimer InitTimerPhysac'
includes ,←⊂ '#include "physac.h"'
includes ,←⊂ '#define InitTimer InitTimer'

functionsStart ← ⍬
functionsStart ,←⊂ '#if defined(__cplusplus)'
functionsStart ,←⊂ 'extern "C" {            // Prevents name mangling of functions'
functionsStart ,←⊂ '#endif'

functionsEnd ← ⍬
functionsEnd ,←⊂ '#if defined(__cplusplus)'
functionsEnd ,←⊂ ,'}'
functionsEnd ,←⊂ '#endif'

filterMacros ← {
  b←'#if defined(GRAPHICS_API_OPENGL_11)'∘≡¨⍵
  e←'#endif'∘≡¨⍵
  k←0
  depth ← e{⍺:k⊢←0 ⋄ ⍵:k⊢←1 ⋄ k}¨b
  ⍵/⍨~depth
}

(rgui physac rl rm rlgl) ← filterMacros¨ (⊃⍤⎕NGET 1,⍨⊂)¨'raygui.h' 'physac.h','../raylib-5.5/src/'∘,¨'raylib.h' 'raymath.h' 'rlgl.h'

⍝ All struct names
sp ← 'typedef struct ' ⍝ Struct prefix
I ← {⍺←⊢⋄⍺ ⍺⍺⍣¯1⊢⍵}

structsAndMore ← rl ⊆⍨ +\(' {'∘≡¨¯2↑¨rl) ∧ sp∘≡¨(≢sp)∘↑¨rl
names ← ' {'∘CTI¨ sp∘CI¨ ⊃¨structsAndMore

⍝ Sizeofs

topCode ←,⊂'char *structNames[] = {','};',⍨ ⊃,/'"'∘,¨,∘'",'¨names
topCode,← ⊂'int structSizes[] = {','};',⍨ ⊃,/'sizeof('∘,¨,∘'),'¨names
topCode,← ⊂''
topCode,← ⊂'#include "IntoFrom.c"'
topCode,← ⊂'#define DECLARE(x) RLAPI x; x'
topCode,← ⊂''
topCode,← ⊂'DECLARE(int StructCount()){return ',';}',⍨⍕≢names
topCode,← ⊂'DECLARE(int GetStructSize(int index)){return structSizes[index];}'
topCode,← ⊂'DECLARE(void GetStructName(char *retName, int strlen, int index)){'
topCode,← ⊂'  for (int i=0;i<strlen;i++) {'
topCode,← ⊂'    retName[i] = structNames[index][i];'
topCode,← ⊂'  }'
topCode,← ⊂,'}'
topCode,← ⊂'DECLARE(int GetStructNameLength(int index)){'
topCode,← ⊂'  char *name = structNames[index];'
topCode,← ⊂'  int acc=0;'
topCode,← ⊂'  while (name[acc] != ''\0'') acc++;'
topCode,← ⊂'  return acc;'
topCode,← ⊂,'}'
topCode,← ⊂'DECLARE(void FloatToDouble(double *n1, float *n2)){'
topCode,← ⊂'  *n1 = *n2;'
topCode,← ⊂,'}'
topCode,← ⊂'DECLARE(void DoubleToFloat(float *n1, double *n2)){'
topCode,← ⊂'  *n1 = *n2;'
topCode,← ⊂,'}'

physacFuncs  ←   physac {⍵CI¨⍺/⍨⍵≡¨10↑¨⍺} ⊂'PHYSACDEF '
rayguiFuncs  ←     rgui {⍵CI¨⍺/⍨⍵≡¨10↑¨⍺} ⊂'RAYGUIAPI '
raylibFuncs  ←       rl {⍵CI¨⍺/⍨⍵≡¨ 6↑¨⍺} ⊂'RLAPI '
raymathFuncs ← ';',⍨¨rm {⍵CI¨⍺/⍨⍵≡¨ 6↑¨⍺} ⊂'RMAPI '

⍝             /¯¯¯¯fixes bug in rl 5.0¯¯¯¯¯¯¯\
rlglFuncs   ← (  rlgl/⍨~∨\functionsEnd⍷  rlgl) {⍵CI¨⍺/⍨⍵≡¨ 6↑¨⍺} ⊂'RLAPI '
physacFuncs ← (physac/⍨~∨\functionsEnd⍷physac) {⍵CI¨⍺/⍨⍵≡¨10↑¨⍺} ⊂'PHYSACDEF '

AddDecl ← {
  p←'DECLARE('⋄s←')'
  p∘,¨⍵/⍨¨1+<\¨s=⍵
}

declorations ← ('h,i'⎕R'h, i') (', \.\.\.\);'⎕R', void *args);')¨⊃,/(raylibFuncs raymathFuncs rlglFuncs rayguiFuncs)
:EndNamespace
